#!/usr/bin/env perl

=pod

=head1 NAME

gnome-overview-conf

=head2 SYNOPSIS

change and view overview configuration

=head2 USAGE

gnome-overview-conf help                # display this
gnome-overview-conf view                # explore current value
gnome-overview-conf import <file>       # import setting from file
gnome-overview-conf export <file>       # export setting to file
sudo gnome-overview-conf <name> <value> # set new <value> for <name> parameter
sudo gnome-overview-conf backup         # backup value
sudo gnome-overview-conf restore        # restore original value

=head2 COPYRIGHT

Copyright vbextreme 2017
License gplv3

=cut

#TODO rewrite read/wrtie multisetting in single line

###########################
###########################
#####     INCLUDE     #####
###########################
###########################

use strict;
use warnings;
#use diagnostics;
use lib '.' ;
use File::Copy;

##########################
##########################
#####     GLOBAL     #####
##########################
##########################

our $VERSION = 0.12;

my $REX_IMPORT ='([a-z_]+)[ \t]*=[ \t]*([0-9]+)';
my $REX_VALUE = '[ \t]*=[ \t]*([0-9]+)';
my $REX_VALUE_SET = '([ \t]*=)([ \t]*)([0-9]+)';
my $REX_ICON_GRID = '\.icon-grid[ \t]*{[ \t\n]*';
my $REX_SPACING = 'spacing:[ \t]*([0-9]+)px;[ \t\n]*';
my $REX_HORIZONTAL = '-shell-grid-horizontal-item-size:[ \t]*([0-9]+)px;[ \t\n]*';
my $REX_VERTICAL = '-shell-grid-vertical-item-size:[ \t]*([0-9]+)px;';
my $REX_CSS = "$REX_ICON_GRID$REX_SPACING$REX_HORIZONTAL$REX_VERTICAL";
my $REX_PANEL_FONT_COLOR = 'panel.solid[ \t\n]*\.panel-button[ \t\n]*{[ \t\n]*color:[ \t\n]*#([A-Fa-f0-9]+)';
my $REX_PANEL_FONT_COLOR_CHECKED = 'panel.solid[ \t\n]*\.panel-button:checked[ \t\n]*{[ \t\n]*color:[ \t\n]*#([A-Fa-f0-9]+)';
my $REX_PANEL_MIN_HEIGHT = 'panel[ \t\n]*{[ \t\n]*min-height:[ \t\n]*([0-9]*)px[; \t\n]*';
my $REX_PANEL_HEIGHT = 'height:[ \t\n]*([0-9.]*)em[; \t\n]*';
my $REX_PANEL_FONT_WEIGHT ='font-weight:[ \t\n]*([0-9]*)';
my $REX_PANEL_CSS = "$REX_PANEL_MIN_HEIGHT$REX_PANEL_HEIGHT$REX_PANEL_FONT_WEIGHT";
my $EXT_BACKUP = '.bak';

my %MATCH = (	max_columns => '((?:const|var) MAX_COLUMNS[ \t]*=)([ \t\n]*)([0-9]+)',
				min_rows => '((?:const|var) MIN_ROWS[ \t]*=)([ \t\n]*)([0-9]+)',
				icon_size => '((?:const|var) ICON_SIZE[ \t]*=)([ \t\n]*)([0-9]+)',
				spacing => '(\.icon-grid[ \t]*{[ \t\n]*spacing:)([ \t\n*])([0-9]+)',
				horizontal => '(\.icon-grid[ \t]*{[ \t\n]*spacing:[ \t]*[0-9]*px\;[ \t\n]*-shell-grid-horizontal-item-size:)([ \t\n]*)([0-9]+)',
				vertical => '(\.icon-grid[ \t]*{[ \t\n]*spacing:[ \t]*[0-9]*px\;[ \t\n]*-shell-grid-horizontal-item-size:[ \t]*[0-9]*px\;[ \t\n]*-shell-grid-vertical-item-size:)([ \t\n]*)([0-9]+)',
				panel_font_color => '(panel.solid[ \t\n]*\.panel-button[ \t\n]*{[ \t\n]*color:[ \t\n]*#)([ \t\n]*)([A-Fa-f0-9]+)',
				panel_font_color_checked => '(panel.solid[ \t\n]*\.panel-button:checked[ \t\n]*{[ \t\n]*color:[ \t\n]*#)([ \t\n]*)([A-Fa-f0-9]+)',
				panel_min_height => '(panel[ \t\n]*{[ \t\n]*min-height:)([ \t\n]*)([0-9]+)',
				panel_height => '(panel[ \t\n]*{[ \t\n]*min-height:[ \t\n]*[0-9]*px;[ \t\n]*height:)([ \t\n]*)([0-9.]+)',
			   	panel_font_weight => '(panel[ \t\n]*{[ \t\n]*min-height:[ \t\n]*[0-9]*px;[ \t\n]*height:[ \t\n]*[0-9.]*em;[ \t\n]*font-weight:)([ \t\n]*)([0-9]+)'
			);

my %shell = (	file32 => '/usr/lib/gnome-shell/libgnome-shell.so',
				backup32 => '/usr/lib/gnome-shell/libgnome-shell.so.bak',
				file =>   '/usr/lib64/gnome-shell/libgnome-shell.so',
				backup => '/usr/lib64/gnome-shell/libgnome-shell.so.bak',
				readSize => 4096,
				buf => ''
			); 

my %theme = (	file => undef,
				backup => undef,
				name => undef,
				dirTheme => '/usr/share/themes',
				userTheme => "/home/$ENV{USER}/.themes",
				themeCSS => 'gnome-shell/gnome-shell.css',
				themeBuiltIn => 'gnome-shell-theme.gresource',
				dirBuiltIn => '/usr/share/gnome-shell',
				readSize => 4096,
				buf => ''
			);

my %SELECT = (	max_columns => \%shell,
				min_rows => \%shell,
				icon_size => \%shell,
				spacing =>    \%theme,
				horizontal => \%theme,
				vertical => \%theme,
				panel_font_color => \%theme,
				panel_font_color_checked => \%theme,
				panel_min_height => \%theme,
				panel_height => \%theme,
				panel_font_weight => \%theme
			);


########################
########################
#####     MAIN     #####
########################
########################

initLibShell();
initTheme();
rawRead(\%shell);
rawRead(\%theme);

usage() if scalar @ARGV < 1 or $ARGV[0] eq 'help';
view() and exit 1 if $ARGV[0] eq 'view';
restore() and exit 1 if $ARGV[0] eq 'restore';
backup() and exit 1 if $ARGV[0] eq 'backup';
timport($ARGV[1]) and exit 1 if $ARGV[0] eq 'import';
texport($ARGV[1]) and exit 1 if $ARGV[0] eq 'export';
waylandcrash() or exit 1 if $ARGV[0] eq 'wayland_restart';

while(@ARGV){
	my $var = shift @ARGV;
	my $val = shift @ARGV;
	
	die "Error input\ncause: no value set\n" unless defined $val;
	die "Error input\ncause: $var is not valid variable\n" unless exists $MATCH{$var};
	setPreserve($SELECT{$var}, $MATCH{$var}, $val);
}

rawWrite(\%shell);
rawWrite(\%theme);

#############################
#############################
#####     INUTILITY     #####
#############################
#############################


sub usage{
	my $us="usage gnome-overview-conf v$VERSION
gnome-overview-conf help                # display this
gnome-overview-conf view                # explore current value
gnome-overview-conf import <file>       # import setting from file
gnome-overview-conf export <file>       # export setting to file
sudo gnome-overview-conf <name> <value> # set new <value> for <name> parameter
sudo gnome-overview-conf backup         # backup value
sudo gnome-overview-conf restore        # restore original value
";
	print "$us";
	exit 1;
}

sub initLibShell{
	unless( -e $shell{file} ){
		die "you use gnome?\ncause:not find libgnome in $shell{file} or $shell{file32}\n" unless -e $shell{file32};
		$shell{file} = $shell{file32};
		$shell{backup} = $shell{backup32};
	}
}

sub initBuiltIn{
	print "use builtin theme\n";
	$theme{file} = "$theme{dirBuiltIn}/$theme{themeBuiltIn}";
	$theme{backup} = "$theme{file}$EXT_BACKUP";
	$theme{name} = 'BuiltIn';
}

sub initTheme{
	my $name = $> ? `gsettings get org.gnome.shell.extensions.user-theme name 2> /dev/null`:`sudo -u "\$USER" gsettings get org.gnome.shell.extensions.user-theme name 2> /dev/null`;

	if (defined $name and $name ne '' ){
		$name = substr($name, 1, length($name)-3);
		
		my $fileusr = "$theme{dirTheme}/$name/$theme{themeCSS}";
		my $filehome = "$theme{userTheme}/$name/$theme{themeCSS}";
		
		if ( -e $fileusr ){
			print "use $name in usr directory\n";
			$theme{name} = $name;
			$theme{file} = $fileusr;
			$theme{backup} = "$fileusr$EXT_BACKUP";
		}
		elsif ( -e $filehome ){
			print "use $name in home directory\n";
			$theme{name} = $name;
			$theme{file} = $filehome;
			$theme{backup} = "$filehome$EXT_BACKUP";
		}
		else{
			initBuiltIn();
		}
	}
	else{
		initBuiltIn();
	}
}

sub rawRead{
	my ($h) = shift;
	$h->{buf} = '';
	open my $fd, "<", $h->{file} or die "cant read $h->{file}\ncause:$!\n";
	binmode($fd);
	my ($buf, $nread);
	while( ($nread = sysread($fd,$buf,$h->{readSize})) > 0 ){
		$h->{buf} .= $buf;
	}
	return 1;
}

sub rawWrite{
	my ($h) = shift;
	open my $fd, "+<", $h->{file} or die "cant write $h->{file}\ncause:$!\n";
	syswrite($fd,$h->{buf});
}

sub loadValue{
	($shell{max_columns}) = $shell{buf} =~ /(?:(?:const|var) MAX_COLUMNS)$REX_VALUE/;
	($shell{min_rows}) = $shell{buf} =~ /(?:(?:const|var) MIN_ROWS)$REX_VALUE/;
	($shell{icon_size}) = $shell{buf} =~ /(?:(?:const|var) ICON_SIZE)$REX_VALUE/;
	($theme{spacing},$theme{horizontal},$theme{vertical}) = $theme{buf} =~ /$REX_CSS/;
	($theme{panel_font_color}) = $theme{buf} =~ /$REX_PANEL_FONT_COLOR/;
	($theme{panel_font_color_checked}) = $theme{buf} =~ /$REX_PANEL_FONT_COLOR_CHECKED/;
	#print "CSS::$REX_PANEL_CSS\n";
	($theme{panel_min_height},$theme{panel_height},$theme{panel_font_weight}) = $theme{buf} =~ /$REX_PANEL_CSS/;
	return 1;
}

sub view{
	loadValue();
	print "max_columns              = $shell{max_columns}\n" if $shell{max_columns};
	print "min_rows                 = $shell{min_rows}\n" if $shell{min_rows};
	print "icon_size                = $shell{icon_size}\n" if $shell{icon_size};
	print "spacing                  = $theme{spacing}\n" if $theme{spacing};
	print "horizontal               = $theme{horizontal}\n" if $theme{horizontal};
	print "vertical                 = $theme{vertical}\n" if $theme{vertical};
	print "panel_font_color         = $theme{panel_font_color}\n" if $theme{panel_font_color};
	print "panel_font_color_checked = $theme{panel_font_color_checked}\n" if $theme{panel_font_color_checked};
	print "panel_min_height         = $theme{panel_min_height}\n" if $theme{panel_min_height};
	print "panel_height             = $theme{panel_height}\n" if $theme{panel_height};
	print "panel_font_weight        = $theme{panel_font_weight}\n" if $theme{panel_font_weight};

	return 1;
}

sub restore{
	copy($shell{backup}, $shell{file}) or die "cant restore $shell{file}\ncause:$!\n";
	copy($theme{backup}, $theme{file}) or die "cant restore $theme{file}\ncause:$1\n";
	print "File restored\n";
	return 1;
}

sub backup{
	die "Error privilege\ncause:you need root\n" if $>;
	print "create theme backup\n";
	copy($theme{file}, $theme{backup}) or die "cant create theme backup\ncause:$!\n";
	print "create shell backup\n";
	copy($shell{file}, $shell{backup}) or die "cant create shell backup\ncause:$!\n";
}

sub setPreserve{
	my ($h, $match, $newValue) = @_;
	#print "MATCH::$match\n";
	my ($unused, $space, $value) = $h->{buf} =~ /$match/;
	#print "UNUSED:$unused\n";
	#print "VALUE:$value\n";
	die "Error regex::$match fail\n" unless $unused or $value;
	my $maxlen = length($space) + length($value);
	my $len = length($newValue);
	return undef if $len > $maxlen;
	my $nspace = $maxlen - $len;
	$space = $nspace ? ' ' x $nspace : '';
	my $replace = "$space$newValue";
	$h->{buf} =~ s/${match}/$1$replace/;
	return 1;
}

sub texport{
	my ($dest) = shift;
	die "Error no file\ncause:you need pass filename\n" unless defined $dest;
	loadValue();
	open my $fd, ">", $dest or die "cant write $dest\ncause:$!\n";
	print $fd "max_columns = $shell{max_columns}\n";
	print $fd "min_rows = $shell{min_rows}\n";
	print $fd "icon_size = $shell{icon_size}\n";
	print $fd "spacing = $theme{spacing}\n";
	print $fd "horizontal = $theme{horizontal}\n";
	print $fd "vertical = $theme{vertical}\n";
	print $fd "panel_font_color = $theme{panel_font_color}\n" if $theme{panel_font_color};
	print $fd "panel_font_color_checked = $theme{panel_font_color_checked}\n" if $theme{panel_font_color_checked};
	print $fd "panel_min_height = $theme{panel_min_height}\n" if $theme{panel_min_height};
	print $fd "panel_height = $theme{panel_height}\n" if $theme{panel_height};
	print $fd "panel_font_weight = $theme{panel_font_weight}\n" if $theme{panel_font_weight};

	return 1;
}

sub timport{
	my ($src) = shift;
	die "Error no file\ncause:you need pass filename\n" unless defined $src;
	open my $fd, "<", $src or die "cant read $src\ncause:$!\n";
	while( my $line = <$fd> ){
		my ($var, $val) = $line =~ /$REX_IMPORT/;
		die "Error import\ncause: no value set\n" unless defined $val;
		die "Error import\ncause: $var is not valid variable\n" unless exists $MATCH{$var};
		setPreserve($SELECT{$var}, $MATCH{$var}, $val);
	}
	close $fd;
	rawWrite(\%shell);
	rawWrite(\%theme);
	return 1;
}

sub copyfk{
	my ($src,$dst) = @_;
	open my $fds, "<", $src or die "cant read $src\ncause:$!\n";
	binmode($fds);
	open my $fdd, ">", $dst or die "cant write $dst\ncause:$!\n";
	binmode($fdd);
	my ($buf, $nread);
	while( ($nread = sysread($fds,$buf,1024)) > 0 ){
		syswrite($fdd,$buf);
	}
	return 1;
}

sub waylandcrash{
	copyfk( $shell{file}, "$shell{file}.crash" );
	copyfk( "$shell{file}.crash", $shell{file} ); 
}
